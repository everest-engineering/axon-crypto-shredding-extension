publishing {
    publications {
        release(MavenPublication) {
            from components.java

            pom {
                name = 'axon-crypto-shredding'
                description = 'Crypto shredding for the Axon event log and saga store.'
                url = 'https://github.com/everest-engineering/axon-crypto-shredding-extension'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'everest-engineering'
                        name = 'EverestEngineering'
                        email = 'hi@everest.engineering'
                    }
                }
                scm {
                    connection = 'https://github.com/everest-engineering/axon-crypto-shredding-extension.git'
                    developerConnection = 'https://github.com/everest-engineering/axon-crypto-shredding-extension.git'
                    url = 'https://github.com/everest-engineering/axon-crypto-shredding-extension'
                }
            }
        }
    }
    repositories {
        maven {
            name = "MavenCentral"
            def ossrhUsername = System.getenv('OSSRH_USERNAME')
            def ossrhPassword = System.getenv('OSSRH_PASSWORD')
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.contains('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = "${ossrhUsername}"
                password = "${ossrhPassword}"
            }
        }
    }
}

signing {
    def signingPassword = System.getenv('SIGNING_PASSWORD')
    def signingKey = System.getenv('SIGNING_KEY')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.release
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
